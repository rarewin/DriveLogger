plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'io.realm.kotlin'
    id 'deploygate'
}

def getVersionName = { variant ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            executable = 'git'
            args = ['describe', '--tag', '--dirty']
            standardOutput = stdout
        }
        return stdout.toString().trim() + "(${variant})"
    } catch (exception) {
        println(exception)
        return "0.0.0-invalid(${variant})"
    }
}

def getVersionCode = { versionName ->
    try {
        def matcher = (versionName =~ /^(?<major>[0-9]+)\.(?<minor>[0-9]+)\.(?<revision>[0-9]+).*/)
        if (!matcher.matches()
                || matcher.group('minor').length() > 2
                || matcher.group('revision').length() > 2) {
            throw new RuntimeException("Invalid version name \"${versionName}\", format \"x.y.z\" expected.")
        }

        return Integer.parseInt(matcher.group('major')) * 10000 \
            + Integer.parseInt(matcher.group('minor')) * 100 \
            + Integer.parseInt(matcher.group('revision'))
    } catch (exception) {
        println(exception)
        return 0
    }
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "org.tirasweel.drivelogger"
        minSdk 29
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'org.tirasweel.drivelogger'
    ndkVersion '24.0.8215888'
    buildToolsVersion '30.0.3'

    applicationVariants.all { variant ->
        def versionTag = getVersionName(variant.buildType.name)
        def myVersionCode = getVersionCode(versionTag)
        def myVersionName = versionTag
        variant.outputs.each { output ->
            println("${variant.name} [${myVersionCode}, ${myVersionName}]")
            output.versionNameOverride = myVersionName
            output.versionCodeOverride = myVersionCode
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'io.realm.kotlin:library-base:1.0.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}